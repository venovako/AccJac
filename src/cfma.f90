PURE FUNCTION CFMA(A, B, C)
  USE, INTRINSIC :: ISO_FORTRAN_ENV, ONLY: REAL32
#ifndef CARITH_PVN
#ifdef USE_IEEE_INTRINSIC
  USE, INTRINSIC :: IEEE_ARITHMETIC, ONLY: IEEE_FMA
#endif
#endif
  IMPLICIT NONE
#ifdef CARITH_PVN
  INTERFACE
     PURE SUBROUTINE PVN_CFMA(DR, DI, AR, AI, BR, BI, CR, CI)
       USE, INTRINSIC :: ISO_C_BINDING, ONLY: c_float
       REAL(KIND=c_float), INTENT(OUT) :: DR, DI
       REAL(KIND=c_float), INTENT(IN) :: AR, AI, BR, BI, CR, CI
     END SUBROUTINE PVN_CFMA
  END INTERFACE
#else
#ifdef USE_IEEE_INTRINSIC
#define SFMA IEEE_FMA
#else
  INTERFACE
     PURE FUNCTION SFMA(A, B, C)
       USE, INTRINSIC :: ISO_FORTRAN_ENV, ONLY: REAL32
       IMPLICIT NONE
       REAL(KIND=REAL32), INTENT(IN) :: A, B, C
       REAL(KIND=REAL32) :: SFMA
     END FUNCTION SFMA
  END INTERFACE
#endif
#endif
  COMPLEX(KIND=REAL32), INTENT(IN) :: A, B, C
  COMPLEX(KIND=REAL32) :: CFMA
#ifdef CARITH_PVN
  REAL(KIND=REAL32) :: AR, AI, BR, BI, CR, CI, DR, DI
  AR = REAL(A)
  AI = AIMAG(A)
  BR = REAL(B)
  BI = AIMAG(B)
  CR = REAL(C)
  CI = AIMAG(C)
  CALL PVN_CFMA(DR, DI, AR, AI, BR, BI, CR, CI)
  CFMA = CMPLX(DR, DI, REAL32)
#else
  ! CFMA = A * B + C
  CFMA = CMPLX(SFMA(REAL(A), REAL(B), SFMA(-AIMAG(A), AIMAG(B), REAL(C))),&
       SFMA(REAL(A), AIMAG(B), SFMA(AIMAG(A), REAL(B), AIMAG(C))), REAL32)
#endif
END FUNCTION CFMA
