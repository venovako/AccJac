FUNCTION ZNRMF(M, X, INFO)
  USE, INTRINSIC :: ISO_FORTRAN_ENV, ONLY: REAL64
  IMPLICIT NONE
  INTERFACE
     PURE FUNCTION CR_HYPOT(X, Y) BIND(C,NAME='cr_hypot')
       USE, INTRINSIC :: ISO_C_BINDING, ONLY: c_double
       IMPLICIT NONE
       REAL(KIND=c_double), INTENT(IN), VALUE :: X, Y
       REAL(KIND=c_double) :: CR_HYPOT
     END FUNCTION CR_HYPOT
  END INTERFACE
  INTEGER, PARAMETER :: K = REAL64
  REAL(KIND=K), PARAMETER :: ZERO = 0.0_K
  INTEGER, INTENT(IN) :: M
  COMPLEX(KIND=K), INTENT(IN) :: X(M)
  INTEGER, INTENT(OUT) :: INFO
  REAL(KIND=K) :: ZNRMF
  INTEGER :: I
  INFO = 0
  ZNRMF = ZERO
  IF (M .LT. 0) INFO = -1
  IF (INFO .NE. 0) RETURN
  IF (M .EQ. 0) RETURN
  ZNRMF = CR_HYPOT(REAL(X(1)), AIMAG(X(1)))
  IF (.NOT. (ZNRMF .LE. HUGE(ZNRMF))) THEN
     INFO = 1
     RETURN
  END IF
  DO I = 2, M
     ZNRMF = CR_HYPOT(ZNRMF, CR_HYPOT(REAL(X(I)), AIMAG(X(I))))
     IF (.NOT. (ZNRMF .LE. HUGE(ZNRMF))) THEN
        INFO = I
        RETURN
     END IF
  END DO
END FUNCTION ZNRMF
