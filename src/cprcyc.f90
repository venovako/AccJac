#ifdef _OPENMP
SUBROUTINE CPRCYC(M, N, G, LDG, JPOS, SV, IX, WRK, RWRK, INFO)
#else
PURE SUBROUTINE CPRCYC(M, N, G, LDG, JPOS, SV, IX, WRK, RWRK, INFO)
#endif
  USE, INTRINSIC :: ISO_FORTRAN_ENV, ONLY: REAL32
  IMPLICIT NONE
  INTERFACE
#ifdef _OPENMP
     SUBROUTINE CCNRMF(M, N, G, LDG, SV, IX, INFO)
#else
     PURE SUBROUTINE CCNRMF(M, N, G, LDG, SV, IX, INFO)
#endif
       USE, INTRINSIC :: ISO_FORTRAN_ENV, ONLY: REAL32
       IMPLICIT NONE
       INTEGER, INTENT(IN) :: M, N, LDG, IX(N)
       COMPLEX(KIND=REAL32), INTENT(IN) :: G(LDG,N)
       REAL(KIND=REAL32), INTENT(OUT) :: SV(N)
       INTEGER, INTENT(OUT) :: INFO
     END SUBROUTINE CCNRMF
  END INTERFACE
  INTERFACE
     PURE SUBROUTINE SNSORT(N, JPOS, SV, IX, INFO)
       USE, INTRINSIC :: ISO_FORTRAN_ENV, ONLY: REAL32
       IMPLICIT NONE
       INTEGER, INTENT(IN) :: N, JPOS
       REAL(KIND=REAL32), INTENT(INOUT) :: SV(N)
       INTEGER, INTENT(INOUT) :: IX(N)
       INTEGER, INTENT(OUT) :: INFO
     END SUBROUTINE SNSORT
  END INTERFACE
  INTEGER, PARAMETER :: K = REAL32
  REAL(KIND=K), PARAMETER :: ZERO = 0.0_K, ONE = 1.0_K
  INTEGER, INTENT(IN) :: M, N, LDG, JPOS
  COMPLEX(KIND=K), INTENT(INOUT) :: G(LDG,N)
  COMPLEX(KIND=K), INTENT(OUT) :: WRK(M,N)
  REAL(KIND=K), INTENT(OUT) :: SV(N), RWRK(N)
  INTEGER, INTENT(INOUT) :: IX(N), INFO
  INTEGER :: I, J
  IF ((JPOS .LT. 0) .OR. (JPOS .GT. N)) INFO = -5
  IF (LDG .LT. M) INFO = -4
  IF (N .LT. 0) INFO = -2
  IF (M .LT. 0) INFO = -1
  IF (INFO .LT. 0) RETURN
  RWRK(N) = ZERO
  IF (INFO .NE. 0) THEN
     IF (INFO .EQ. 2) THEN
        DO J = 1, N
           DO I = 1, M
              WRK(I,J) = ZERO
           END DO
        END DO
        DO I = 1, N-1
           RWRK(I) = ONE
        END DO
     ELSE IF (INFO .NE. 1) THEN
        INFO = -9
        RETURN
     END IF
     CALL CCNRMF(M, N, G, LDG, SV, IX, INFO)
     IF (INFO .NE. 0) THEN
        INFO = -6
        RETURN
     END IF
  END IF
  CALL SNSORT(N, JPOS, SV, IX, INFO)
  IF (INFO .LT. 0) THEN
     INFO = -7
     RETURN
  END IF
END SUBROUTINE CPRCYC
