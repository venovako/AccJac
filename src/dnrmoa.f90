PURE FUNCTION DNRMOA(N, A, LDA, AS)
#ifdef __GFORTRAN__
  USE, INTRINSIC :: ISO_C_BINDING, ONLY: c_long_double
  USE, INTRINSIC :: ISO_FORTRAN_ENV, ONLY: REAL64
#else
  USE, INTRINSIC :: ISO_FORTRAN_ENV, ONLY: REAL64, REAL128
#endif
  IMPLICIT NONE
  INTERFACE
#ifdef __GFORTRAN__
     PURE FUNCTION XSQRT(X) BIND(C,NAME='sqrtl')
       USE, INTRINSIC :: ISO_C_BINDING, ONLY: c_long_double
#else
     PURE FUNCTION XSQRT(X) BIND(C,NAME='cr_sqrtq')
       USE, INTRINSIC :: ISO_FORTRAN_ENV, ONLY: REAL128
#endif
       IMPLICIT NONE
#ifdef __GFORTRAN__
       REAL(KIND=c_long_double), INTENT(IN), VALUE :: X
       REAL(KIND=c_long_double) :: XSQRT
#else
       REAL(KIND=REAL128), INTENT(IN), VALUE :: X
       REAL(KIND=REAL128) :: XSQRT
#endif
     END FUNCTION XSQRT
  END INTERFACE
#ifdef __GFORTRAN__
  INTERFACE
     PURE FUNCTION HYPOTX(X, Y) BIND(C,NAME='cr_hypotl')
       USE, INTRINSIC :: ISO_C_BINDING, ONLY: c_long_double
       IMPLICIT NONE
       REAL(KIND=c_long_double), INTENT(IN), VALUE :: X, Y
       REAL(KIND=c_long_double) :: HYPOTX
     END FUNCTION HYPOTX
  END INTERFACE
  INTEGER, PARAMETER :: KK = c_long_double
#else
#define HYPOTX HYPOT
  INTEGER, PARAMETER :: KK = REAL128
#endif
  INTEGER, PARAMETER :: K = REAL64
  REAL(KIND=KK), PARAMETER :: ZERO = 0.0_KK
  INTEGER, INTENT(IN) :: N, LDA, AS
  REAL(KIND=K), INTENT(IN) :: A(LDA,N)
  REAL(KIND=KK) :: DNRMOA
  INTEGER :: I, J, L
  DNRMOA = ZERO
  IF (N .LE. 1) RETURN
  L = -AS
  DO J = 1, N-1
     DO I = J+1, N
        DNRMOA = HYPOTX(DNRMOA, SCALE(REAL(A(I,J), KK), L))
     END DO
  END DO
  DNRMOA = DNRMOA * XSQRT(2.0_KK)
END FUNCTION DNRMOA
