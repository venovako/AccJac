PURE FUNCTION ZFMA(A, B, C)
  USE, INTRINSIC :: ISO_FORTRAN_ENV, ONLY: REAL64
  IMPLICIT NONE
#ifdef CARITH_PVN
  INTERFACE
     PURE SUBROUTINE PVN_ZFMA(DR, DI, AR, AI, BR, BI, CR, CI)
       USE, INTRINSIC :: ISO_C_BINDING, ONLY: c_double
       REAL(KIND=c_double), INTENT(OUT) :: DR, DI
       REAL(KIND=c_double), INTENT(IN) :: AR, AI, BR, BI, CR, CI
     END SUBROUTINE PVN_ZFMA
  END INTERFACE
#else
  INTERFACE
     PURE FUNCTION DFMA(A, B, C)
       USE, INTRINSIC :: ISO_FORTRAN_ENV, ONLY: REAL64
       IMPLICIT NONE
       REAL(KIND=REAL64), INTENT(IN) :: A, B, C
       REAL(KIND=REAL64) :: DFMA
     END FUNCTION DFMA
  END INTERFACE
#endif
  COMPLEX(KIND=REAL64), INTENT(IN) :: A, B, C
  COMPLEX(KIND=REAL64) :: ZFMA
#ifdef CARITH_PVN
  REAL(KIND=REAL64) :: AR, AI, BR, BI, CR, CI, DR, DI
  AR = REAL(A)
  AI = AIMAG(A)
  BR = REAL(B)
  BI = AIMAG(B)
  CR = REAL(C)
  CI = AIMAG(C)
  CALL PVN_ZFMA(DR, DI, AR, AI, BR, BI, CR, CI)
  ZFMA = CMPLX(DR, DI, REAL64)
#else
  ! ZFMA = A * B + C
  ZFMA = CMPLX(DFMA(REAL(A), REAL(B), DFMA(-AIMAG(A), AIMAG(B), REAL(C))),&
       DFMA(REAL(A), AIMAG(B), DFMA(AIMAG(A), REAL(B), AIMAG(C))), REAL64)
#endif
END FUNCTION ZFMA
