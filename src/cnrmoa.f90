PURE FUNCTION CNRMOA(N, A, LDA, AS)
  USE, INTRINSIC :: ISO_FORTRAN_ENV, ONLY: REAL32, REAL64
  IMPLICIT NONE
  INTERFACE
     PURE FUNCTION HYPOTX(X, Y) BIND(C,NAME='cr_hypot')
       USE, INTRINSIC :: ISO_C_BINDING, ONLY: c_double
       IMPLICIT NONE
       REAL(KIND=c_double), INTENT(IN), VALUE :: X, Y
       REAL(KIND=c_double) :: HYPOTX
     END FUNCTION HYPOTX
  END INTERFACE
  INTEGER, PARAMETER :: KK = REAL64
  INTEGER, PARAMETER :: K = REAL32
  REAL(KIND=KK), PARAMETER :: ZERO = 0.0_KK, SQRT2 = SQRT(2.0_KK)
  INTEGER, INTENT(IN) :: N, LDA, AS
  COMPLEX(KIND=K), INTENT(IN) :: A(LDA,N)
  REAL(KIND=KK) :: CNRMOA
  INTEGER :: I, J, L
  CNRMOA = ZERO
  IF (N .LE. 1) RETURN
  L = -AS
  DO J = 1, N-1
     DO I = J+1, N
        CNRMOA = HYPOTX(CNRMOA, HYPOTX(SCALE(REAL(REAL(A(I,J)), KK), L), SCALE(REAL(AIMAG(A(I,J)), KK), L)))
     END DO
  END DO
  CNRMOA = CNRMOA * SQRT2
END FUNCTION CNRMOA
