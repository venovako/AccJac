PURE SUBROUTINE ZPRCYC(M, N, G, LDG, JPOS, SV, IX, WRK, INFO)
  USE, INTRINSIC :: ISO_FORTRAN_ENV, ONLY: REAL64
  IMPLICIT NONE
  INTERFACE
     PURE SUBROUTINE ZCNRMF(M, N, G, LDG, SV, IX, INFO)
       USE, INTRINSIC :: ISO_FORTRAN_ENV, ONLY: REAL64
       IMPLICIT NONE
       INTEGER, INTENT(IN) :: M, N, LDG, IX(N)
       COMPLEX(KIND=REAL64), INTENT(IN) :: G(LDG,N)
       REAL(KIND=REAL64), INTENT(OUT) :: SV(N)
       INTEGER, INTENT(OUT) :: INFO
     END SUBROUTINE ZCNRMF
  END INTERFACE
  INTERFACE
     PURE SUBROUTINE DNSORT(N, JPOS, SV, IX, INFO)
       USE, INTRINSIC :: ISO_FORTRAN_ENV, ONLY: REAL64
       IMPLICIT NONE
       INTEGER, INTENT(IN) :: N, JPOS
       REAL(KIND=REAL64), INTENT(INOUT) :: SV(N)
       INTEGER, INTENT(INOUT) :: IX(N)
       INTEGER, INTENT(OUT) :: INFO
     END SUBROUTINE DNSORT
  END INTERFACE
  INTEGER, PARAMETER :: K = REAL64
  REAL(KIND=K), PARAMETER :: ZERO = 0.0_K, ONE = 1.0_K
  INTEGER, INTENT(IN) :: M, N, LDG, JPOS
  COMPLEX(KIND=K), INTENT(INOUT) :: G(LDG,N), WRK(M,N)
  REAL(KIND=K), INTENT(OUT) :: SV(N)
  INTEGER, INTENT(INOUT) :: IX(N), INFO
  INTEGER :: I, J
#ifndef NDEBUG
  IF ((JPOS .LT. 0) .OR. (JPOS .GT. N)) INFO = -5
  IF (LDG .LT. M) INFO = -4
  IF (N .LT. 0) INFO = -2
  IF (M .LT. 0) INFO = -1
  IF (INFO .LT. 0) RETURN
#endif
  IF (INFO .NE. 0) THEN
     IF (INFO .EQ. 2) THEN
        DO J = 1, N-1
           DO I = 1, M
              WRK(I,J) = ZERO
           END DO
        END DO
        DO I = 1, M
           WRK(I,N) = ONE
        END DO
#ifndef NDEBUG
     ELSE IF (INFO .NE. 1) THEN
        INFO = -9
        RETURN
#endif
     END IF
     CALL ZCNRMF(M, N, G, LDG, SV, IX, INFO)
#ifndef NDEBUG
     IF (INFO .NE. 0) THEN
        INFO = -6
        RETURN
     END IF
#endif
  END IF
  CALL DNSORT(N, JPOS, SV, IX, INFO)
#ifndef NDEBUG
  IF (INFO .LT. 0) THEN
     INFO = -7
     RETURN
  END IF
#endif
END SUBROUTINE ZPRCYC
