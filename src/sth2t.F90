!!! QUADRUPLE PRECISION BEHAVES DIFFERENTLY WITH THE GNU FORTRAN AND WITH THE INTEL FORTRAN !!!
! MPFR is used instead of REAL128 !
PROGRAM STH2T
  USE, INTRINSIC :: ISO_FORTRAN_ENV, ONLY: OUTPUT_UNIT, INT32, REAL32, REAL64
  IMPLICIT NONE
  INTERFACE
     PURE FUNCTION DFMA(A, B, C)
       USE, INTRINSIC :: ISO_FORTRAN_ENV, ONLY: REAL64
       IMPLICIT NONE
       REAL(KIND=REAL64), INTENT(IN) :: A, B, C
       REAL(KIND=REAL64) :: DFMA
     END FUNCTION DFMA
  END INTERFACE
  REAL(KIND=REAL32), PARAMETER :: ZERO = 0.0_REAL32, ONE = 1.0_REAL32, TWO = 2.0_REAL32
  REAL(KIND=REAL32), PARAMETER :: CUTOFF = 40.0_REAL32 / 41.0_REAL32, EPS = EPSILON(ZERO) / TWO
  REAL(KIND=REAL64), PARAMETER :: DZERO = 0.0_REAL64
  INTEGER, PARAMETER :: ETH = 1, ECH = 2, ESH = 3, STH = 4, SCH = 5, SSH = 6, ARE = 1, MRE = 2
  CHARACTER(LEN=256) :: CLA
  REAL(KIND=REAL64) :: Q(2,6), QD, QT, QC, QS, J
  REAL(KIND=REAL32) :: D, T, C, S, U
  INTEGER :: I, NEXP
  INTEGER(KIND=INT32) :: ID
  EQUIVALENCE (D,ID)
  EXTERNAL :: MPFR_START, MPFR_STOP, TCS_OLD, TCS_NEW
  IF (COMMAND_ARGUMENT_COUNT() .NE. 1) STOP 'sth2t.exe EXP'
  CALL GET_COMMAND_ARGUMENT(1, CLA)
  READ (CLA,*) NEXP
  IF (NEXP .GE. 0) STOP 'EXP must be < 0'
  Q = DZERO
  IF (NEXP .EQ. -1) THEN
     U = CUTOFF
  ELSE ! NEXP < -1
     U = SCALE(ONE, NEXP + 1)
  END IF
  D = SCALE(ONE, NEXP)
  I = 1024
  CALL MPFR_START(I, EPS)
  IF (I .NE. 0) STOP 'MPFR_START'
  I = 1
  DO WHILE (D .LT. U)
     CALL TCS_NEW(D, T, C, S, QT, QC, QS)
     QD = I - 1
     J = I
     QD = QD / J
     Q(ARE,ETH) = DFMA(Q(ARE,ETH), QD, (QT / J))
     Q(MRE,ETH) = MAX(Q(MRE,ETH), QT)
     Q(ARE,ECH) = DFMA(Q(ARE,ECH), QD, (QC / J))
     Q(MRE,ECH) = MAX(Q(MRE,ECH), QC)
     Q(ARE,ESH) = DFMA(Q(ARE,ESH), QD, (QS / J))
     Q(MRE,ESH) = MAX(Q(MRE,ESH), QS)
     ! "standard" formulas
     CALL TCS_OLD(D, T, C, S, QT, QC, QS)
     Q(ARE,STH) = DFMA(Q(ARE,STH), QD, (QT / J))
     Q(MRE,STH) = MAX(Q(MRE,STH), QT)
     Q(ARE,SCH) = DFMA(Q(ARE,SCH), QD, (QC / J))
     Q(MRE,SCH) = MAX(Q(MRE,SCH), QC)
     Q(ARE,SSH) = DFMA(Q(ARE,SSH), QD, (QS / J))
     Q(MRE,SSH) = MAX(Q(MRE,SSH), QS)
     ! increment
     ID = ID + 1
     I = I + 1
  END DO
  CALL MPFR_STOP()
  ! relative errors in the terms of \epsilon
  I = I - 1
  WRITE (OUTPUT_UNIT,'(I3,A,I11,12(A,ES16.9E2))') NEXP, ',', I, ',',&
       Q(ARE,ETH), ',', Q(MRE,ETH), ',', Q(ARE,ECH), ',', Q(MRE,ECH), ',',&
       Q(ARE,ESH), ',', Q(MRE,ESH), ',', Q(ARE,STH), ',', Q(MRE,STH), ',',&
       Q(ARE,SCH), ',', Q(MRE,SCH), ',', Q(ARE,SSH), ',', Q(MRE,SSH)
END PROGRAM STH2T
