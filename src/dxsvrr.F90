PROGRAM DXSVRR
#ifdef __GFORTRAN__
  USE, INTRINSIC :: ISO_C_BINDING, ONLY: c_long_double
  USE, INTRINSIC :: ISO_FORTRAN_ENV, ONLY: OUTPUT_UNIT, REAL64
#else
  USE, INTRINSIC :: ISO_FORTRAN_ENV, ONLY: OUTPUT_UNIT, REAL64, REAL128
#endif
  IMPLICIT NONE
  INTEGER, PARAMETER :: K = REAL64
#ifdef __GFORTRAN__
  INTEGER, PARAMETER :: KK = c_long_double
#else
  INTEGER, PARAMETER :: KK = REAL128
#endif
  CHARACTER(LEN=256) :: CLA
  REAL(KIND=KK) :: Z
  INTEGER :: N, I, O
  REAL(KIND=K), ALLOCATABLE :: S(:)
  REAL(KIND=KK), ALLOCATABLE :: X(:)
  EXTERNAL :: BFOPEN, XBRD1
  CALL GET_COMMAND_ARGUMENT(0, CLA)
  IF (COMMAND_ARGUMENT_COUNT() .NE. 2) STOP TRIM(CLA)//' N FN'
  CALL GET_COMMAND_ARGUMENT(1, CLA)
  READ (CLA,*) N
  IF (N .LE. 0) STOP 'N'
  CALL GET_COMMAND_ARGUMENT(2, CLA)
  CALL BFOPEN(TRIM(CLA)//'.SX', 'RO', I, O)
  IF (O .NE. 0) STOP 'OPEN(SX)'
  ALLOCATE(X(N))
  CALL XBRD1(I, N, X, O)
  IF (O .NE. 0) STOP 'READ(SX)'
  CLOSE (UNIT=I, IOSTAT=O)
  IF (O .NE. 0) STOP 'CLOSE(SX)'
  CALL BFOPEN(TRIM(CLA)//'.SY', 'RO', I, O)
  IF (O .NE. 0) STOP 'OPEN(SY)'
  ALLOCATE(S(N))
  READ (UNIT=I, IOSTAT=O) S
  IF (O .NE. 0) STOP 'READ(SY)'
  CLOSE (UNIT=I, IOSTAT=O)
  IF (O .NE. 0) STOP 'CLOSE(SY)'
  Z = 0.0_KK
  O = 0
  DO I = 1, N
     X(I) = ABS(X(I) - S(I)) / X(I)
     S(I) = REAL(X(I), K)
     IF (X(I) .GT. Z) THEN
        Z = X(I)
        O = I
     END IF
     WRITE (OUTPUT_UNIT,9) I, S(I)
  END DO
  WRITE (OUTPUT_UNIT,9) -O, REAL(Z, K)
  DEALLOCATE(S)
  DEALLOCATE(X)
9 FORMAT(I11,ES25.17E3)
END PROGRAM DXSVRR
