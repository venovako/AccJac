! IN: INFO & 1: sin => tan
!     INFO & 2: hyp
!OUT: INFO = 0: notransf
!     INFO = 1: swap only
!     INFO = 2: transf, no downscaling of G and SV
!     INFO = 3: transf with downscaling of G and SV
SUBROUTINE CTRANS(M, N, G, LDG, V, LDV, SV, GX, GS, P, Q, TOL, INFO)
  USE, INTRINSIC :: ISO_FORTRAN_ENV, ONLY: REAL32
  IMPLICIT NONE
  INTERFACE
     PURE FUNCTION CR_HYPOT(X, Y) BIND(C,NAME='cr_hypotf')
       USE, INTRINSIC :: ISO_C_BINDING, ONLY: c_float
       IMPLICIT NONE
       REAL(KIND=c_float), INTENT(IN), VALUE :: X, Y
       REAL(KIND=c_float) :: CR_HYPOT
     END FUNCTION CR_HYPOT
  END INTERFACE
  INTERFACE
     FUNCTION CSDP(M, X, Y, MX, MY, INFO)
       USE, INTRINSIC :: ISO_FORTRAN_ENV, ONLY: REAL32
       IMPLICIT NONE
       INTEGER, INTENT(IN) :: M
       COMPLEX(KIND=REAL32), INTENT(IN) :: X(M), Y(M)
       REAL(KIND=REAL32), INTENT(IN) :: MX, MY
       INTEGER, INTENT(OUT) :: INFO
       COMPLEX(KIND=REAL32) :: CSDP
     END FUNCTION CSDP
  END INTERFACE
  INTERFACE
     PURE SUBROUTINE CGRAM(PNF, QNF, QPS, APP, AQQ, AQPR, AQPI, INFO)
       USE, INTRINSIC :: ISO_FORTRAN_ENV, ONLY: REAL32
       IMPLICIT NONE
       REAL(KIND=REAL32), INTENT(IN) :: PNF, QNF
       COMPLEX(KIND=REAL32), INTENT(IN) :: QPS
       REAL(KIND=REAL32), INTENT(OUT) :: APP, AQQ, AQPR, AQPI
       INTEGER, INTENT(OUT) :: INFO
     END SUBROUTINE CGRAM
  END INTERFACE
  INTERFACE
     SUBROUTINE CLJU2(A11, A22, A21R, A21I, CS, SNR, SNI, INFO)
       USE, INTRINSIC :: ISO_FORTRAN_ENV, ONLY: REAL32
       IMPLICIT NONE
       REAL(KIND=REAL32), INTENT(IN) :: A11, A22, A21R, A21I
       REAL(KIND=REAL32), INTENT(OUT) :: CS, SNR, SNI
       INTEGER, INTENT(INOUT) :: INFO
     END SUBROUTINE CLJU2
  END INTERFACE
  INTERFACE
     SUBROUTINE CLJV2(A11, A22, A21R, A21I, CH, SHR, SHI, INFO)
       USE, INTRINSIC :: ISO_FORTRAN_ENV, ONLY: REAL32
       IMPLICIT NONE
       REAL(KIND=REAL32), INTENT(IN) :: A11, A22, A21R, A21I
       REAL(KIND=REAL32), INTENT(OUT) :: CH, SHR, SHI
       INTEGER, INTENT(INOUT) :: INFO
     END SUBROUTINE CLJV2
  END INTERFACE
  INTERFACE
     PURE SUBROUTINE CRTVT(M, X, Y, CS, SNR, SNI, INFO)
       USE, INTRINSIC :: ISO_FORTRAN_ENV, ONLY: REAL32
       IMPLICIT NONE
       INTEGER, INTENT(IN) :: M
       COMPLEX(KIND=REAL32), INTENT(INOUT) :: X(M), Y(M)
       REAL(KIND=REAL32), INTENT(IN) :: CS, SNR, SNI
       INTEGER, INTENT(INOUT) :: INFO
     END SUBROUTINE CRTVT
  END INTERFACE
  INTERFACE
     PURE SUBROUTINE CRTVH(M, X, Y, CH, SHR, SHI, INFO)
       USE, INTRINSIC :: ISO_FORTRAN_ENV, ONLY: REAL32
       IMPLICIT NONE
       INTEGER, INTENT(IN) :: M
       COMPLEX(KIND=REAL32), INTENT(INOUT) :: X(M), Y(M)
       REAL(KIND=REAL32), INTENT(IN) :: CH, SHR, SHI
       INTEGER, INTENT(INOUT) :: INFO
     END SUBROUTINE CRTVH
  END INTERFACE
  INTERFACE
     PURE SUBROUTINE CROTT(M, X, Y, CS, SNR, SNI, GX, MX, MY, INFO)
       USE, INTRINSIC :: ISO_FORTRAN_ENV, ONLY: REAL32
       IMPLICIT NONE
       INTEGER, INTENT(IN) :: M
       COMPLEX(KIND=REAL32), INTENT(INOUT) :: X(M), Y(M)
       REAL(KIND=REAL32), INTENT(IN) :: CS, SNR, SNI
       REAL(KIND=REAL32), INTENT(INOUT) :: GX, MX, MY
       INTEGER, INTENT(INOUT) :: INFO
     END SUBROUTINE CROTT
  END INTERFACE
  INTERFACE
     PURE SUBROUTINE CROTH(M, X, Y, CH, SHR, SHI, GX, MX, MY, INFO)
       USE, INTRINSIC :: ISO_FORTRAN_ENV, ONLY: REAL32
       IMPLICIT NONE
       INTEGER, INTENT(IN) :: M
       COMPLEX(KIND=REAL32), INTENT(INOUT) :: X(M), Y(M)
       REAL(KIND=REAL32), INTENT(IN) :: CH, SHR, SHI
       REAL(KIND=REAL32), INTENT(INOUT) :: GX, MX, MY
       INTEGER, INTENT(INOUT) :: INFO
     END SUBROUTINE CROTH
  END INTERFACE
  INTERFACE
     PURE SUBROUTINE CSCALG(M, N, G, LDG, GX, GS, INFO)
       USE, INTRINSIC :: ISO_FORTRAN_ENV, ONLY: REAL32
       IMPLICIT NONE
       INTEGER, INTENT(IN) :: M, N, LDG
       COMPLEX(KIND=REAL32), INTENT(INOUT) :: G(LDG,N)
       REAL(KIND=REAL32), INTENT(INOUT) :: GX
       INTEGER, INTENT(INOUT) :: GS, INFO
     END SUBROUTINE CSCALG
  END INTERFACE
  INTEGER, PARAMETER :: K = REAL32
  REAL(KIND=K), PARAMETER :: ZERO = 0.0_K
  INTEGER, INTENT(IN) :: M, N, LDG, LDV, P, Q
  COMPLEX(KIND=K), INTENT(INOUT) :: G(LDG,N), V(LDV,N)
  REAL(KIND=K), INTENT(INOUT) :: SV(N), GX
  REAL(KIND=K), INTENT(IN) :: TOL
  INTEGER, INTENT(INOUT) :: GS, INFO
  COMPLEX(KIND=K) :: QPS, Z
  REAL(KIND=K) :: APP, AQQ, AQPR, AQPI, C, SR, SI, T
  INTEGER :: I, J, L
  IF ((INFO .LT. 0) .OR. (INFO .GT. 3)) INFO = -13
  IF (TOL .LT. ZERO) INFO = -12
  IF ((Q .LE. 0) .OR. (Q .GT. N)) INFO = -11
  IF ((P .LE. 0) .OR. (P .GT. N)) INFO = -10
  IF (LDV .LT. N) INFO = -6
  IF (LDG .LT. M) INFO = -4
  IF ((N .LT. 0) .OR. (N .GT. M)) INFO = -2
  IF (M .LT. 0) INFO = -1
  IF (INFO .LT. 0) RETURN
  IF (M .EQ. 0) RETURN
  I = 0
  QPS = CSDP(M, G(1,Q), G(1,P), SV(Q), SV(P), I)
  IF (I .NE. 0) THEN
     INFO = -3
     RETURN
  END IF
  T = CR_HYPOT(REAL(QPS), AIMAG(QPS))
  IF (.NOT. (T .LE. HUGE(T))) THEN
     INFO = -3
     RETURN
  END IF
  L = INFO
  IF (T .LT. TOL) THEN
     IF ((IAND(L,2) .EQ. 0) .AND. (SV(P) .LT. SV(Q))) THEN
        DO I = 1, M
           Z = G(I,P)
           G(I,P) = G(I,Q)
           G(I,Q) = Z
        END DO
        DO I = 1, N
           Z = V(I,P)
           V(I,P) = V(I,Q)
           V(I,Q) = Z
        END DO
        T = SV(P)
        SV(P) = SV(Q)
        SV(Q) = T
        INFO = 1
     ELSE ! no swap
        INFO = 0
     END IF
     RETURN
  ELSE ! transform
     INFO = 2
  END IF
  J = 0
  CALL CGRAM(SV(P), SV(Q), QPS, APP, AQQ, AQPR, AQPI, J)
  IF (J .LE. -HUGE(J)) THEN
     INFO = -7
     RETURN
  END IF
  J = IAND(L, 2)
  I = IAND(L, 1)
  IF (IAND(L, 4) .NE. 0) I = IOR(I, 16)
  T = GX
  IF (J .EQ. 0) THEN
     CALL CLJU2(APP, AQQ, AQPR, AQPI, C, SR, SI, I)
     CALL CROTT(M, G(1,P), G(1,Q), C, SR, SI, T, SV(P), SV(Q), I)
     CALL CRTVT(N, V(1,P), V(1,Q), C, SR, SI, I)
  ELSE ! hyp
     CALL CLJV2(APP, AQQ, AQPR, AQPI, C, SR, SI, I)
     CALL CROTH(M, G(1,P), G(1,Q), C, SR, SI, T, SV(P), SV(Q), I)
     CALL CRTVH(N, V(1,P), V(1,Q), C, SR, SI, I)
  END IF
  IF (I .LT. 0) THEN
     INFO = -8
  ELSE IF (IAND(I, 4) .NE. 0) THEN
     IF (IAND(I, 8) .EQ. 0) THEN
        INFO = 0
     ELSE ! swap
        INFO = 1
     END IF
  ELSE IF (T .GT. GX) THEN
     GX = T
     I = 1
     CALL CSCALG(M, N, G, LDG, GX, GS, I)
     IF (I .LT. 0) THEN
        INFO = -9
        RETURN
     END IF
     IF (I .GT. 0) THEN
        I = -I
        DO J = 1, N
           SV(J) = SCALE(SV(J), I)
        END DO
        INFO = 3
     END IF
  END IF
END SUBROUTINE CTRANS
