PROGRAM CJSVDT
  USE, INTRINSIC :: IEEE_ARITHMETIC, ONLY: IEEE_FMA
  USE, INTRINSIC :: ISO_C_BINDING, ONLY: c_long
  USE, INTRINSIC :: ISO_FORTRAN_ENV, ONLY: OUTPUT_UNIT, REAL32, REAL128
  IMPLICIT NONE
  REAL(KIND=REAL128), PARAMETER :: QZERO = 0.0_REAL128, QONE = 1.0_REAL128
  CHARACTER(LEN=256) :: CLA
  REAL(KIND=REAL128) :: Y, Z
  COMPLEX(KIND=REAL128) :: H
  COMPLEX(KIND=REAL32) :: T
  INTEGER :: M, N, LDG, LDV, JPOS, GS, INFO, I, J, L
  COMPLEX(KIND=REAL32), ALLOCATABLE :: G(:,:), V(:,:), WRK(:,:)
  REAL(KIND=REAL32), ALLOCATABLE :: SV(:)
  COMPLEX(KIND=REAL128), ALLOCATABLE :: U(:,:), W(:,:)
  INTEGER(KIND=c_long), EXTERNAL :: PVN_TIME_MONO_NS
  EXTERNAL :: CJSVDC, CJSVDR
  ! read the command line arguments
  I = COMMAND_ARGUMENT_COUNT()
  IF (I .NE. 5) STOP 'cjsvdt.exe M N JPOS OPTS FILE'
  CALL GET_COMMAND_ARGUMENT(1, CLA)
  READ (CLA,*) M
  IF (M .LT. 0) THEN
     M = -M
     Z = QONE
  ELSE IF (M .GT. 0) THEN
     Z = QZERO
  ELSE ! M = 0
     STOP 'M'
  END IF
  CALL GET_COMMAND_ARGUMENT(2, CLA)
  READ (CLA,*) N
  IF ((N .LE. 0) .OR. (N .GT. M)) STOP 'N'
  CALL GET_COMMAND_ARGUMENT(3, CLA)
  READ (CLA,*) JPOS
  IF ((JPOS .LT. 0) .OR. (JPOS .GT. N)) STOP 'JPOS'
  CALL GET_COMMAND_ARGUMENT(4, CLA)
  READ (CLA,*) L
  IF (L .LT. 0) STOP 'OPTS'
  CALL GET_COMMAND_ARGUMENT(5, CLA)
  IF (LEN_TRIM(CLA) .LE. 0) STOP 'FILE'
  ! set G
  LDG = M
  ALLOCATE(G(LDG,N))
  CALL BFOPEN(TRIM(CLA)//'.Y', 'RO', I, J)
  IF (J .NE. 0) STOP 'Y'
  READ (UNIT=I, IOSTAT=J) G
  IF (J .NE. 0) STOP 'G'
  CLOSE(I)
  ! allocate the rest
  LDV = N
  ALLOCATE(V(LDV,N))
  ALLOCATE(WRK(M,N))
  ALLOCATE(SV(N))
  ! call CJSVDC
  GS = HUGE(GS)
  INFO = IAND(L, 3)
  Y = PVN_TIME_MONO_NS()
  IF (IAND(L, 4) .EQ. 0) THEN
     CALL CJSVDC(M, N, G, LDG, V, LDV, JPOS, SV, WRK, GS, INFO)
  ELSE ! CJSVDR
     CALL CJSVDR(M, N, G, LDG, V, LDV, JPOS, SV, WRK, GS, INFO)
  END IF
  Y = (PVN_TIME_MONO_NS() - Y) / 1000000000
  WRITE (OUTPUT_UNIT,'(I11,A,I6,A,F15.6,A)',ADVANCE='NO') INFO, ',', GS, ',', Y, ','
  FLUSH(OUTPUT_UNIT)
  IF (INFO .LT. 0) THEN
     IF (IAND(L, 4) .EQ. 0) THEN
        STOP 'CJSVDC'
     ELSE ! CJSVDR
        STOP 'CJSVDR'
     END IF
  END IF
  CALL BFOPEN(TRIM(CLA)//'.YU', 'WO', I, J)
  IF (J .NE. 0) STOP 'YU'
  WRITE (UNIT=I, IOSTAT=J) G
  IF (J .NE. 0) STOP 'U'
  CLOSE(I)
  CALL BFOPEN(TRIM(CLA)//'.YV', 'WO', I, J)
  IF (J .NE. 0) STOP 'YV'
  WRITE (UNIT=I, IOSTAT=J) V
  IF (J .NE. 0) STOP 'V'
  CLOSE(I)
  CALL BFOPEN(TRIM(CLA)//'.SS', 'WO', I, J)
  IF (J .NE. 0) STOP 'SS'
  WRITE (UNIT=I, IOSTAT=J) SV
  IF (J .NE. 0) STOP 'SV'
  CLOSE(I)
  ! V^-1 = J V^H J
  DO J = 1, N
     DO I = 1, J-1
        T = V(I,J)
        V(I,J) = CONJG(V(J,I))
        V(J,I) = CONJG(T)
     END DO
     V(J,J) = CONJG(V(J,J))
  END DO
  DO J = 1, JPOS
     DO I = JPOS+1, N
        V(I,J) = -V(I,J)
     END DO
  END DO
  DO J = JPOS+1, N
     DO I = 1, JPOS
        V(I,J) = -V(I,J)
     END DO
  END DO
  CALL BFOPEN(TRIM(CLA)//'.ZZ', 'WO', I, J)
  IF (J .NE. 0) STOP 'ZZ'
  WRITE (UNIT=I, IOSTAT=J) V
  IF (J .NE. 0) STOP 'Z'
  CLOSE(I)
  L = -GS
  IF (Z .EQ. QZERO) THEN
     ALLOCATE(U(M,N))
     DO J = 1, N
        Y = SV(J)
        Y = SCALE(Y, L)
        DO I = 1, M
           U(I,J) = CMPLX(&
                REAL(REAL(G(I,J)), REAL128) * Y,&
                REAL(AIMAG(G(I,J)), REAL128) * Y, REAL128)
        END DO
     END DO
  END IF
  DO J = 1, N
     SV(J) = SCALE(SV(J), L)
  END DO
  CALL BFOPEN(TRIM(CLA)//'.SY', 'WO', I, J)
  IF (J .NE. 0) STOP 'SY'
  WRITE (UNIT=I, IOSTAT=J) SV
  IF (J .NE. 0) STOP 'S'
  Y = QZERO
  IF (Z .EQ. QZERO) THEN
     ! read G again
     CALL BFOPEN(TRIM(CLA)//'.Y', 'RO', I, J)
     IF (J .NE. 0) STOP 'Y'
     READ (UNIT=I, IOSTAT=J) G
     IF (J .NE. 0) STOP 'G'
     CLOSE(I)
     ALLOCATE(W(M,N))
     DO I = 1, M
        DO J = 1, N
           W(I,J) = G(I,J)
           Z = HYPOT(Z, HYPOT(REAL(W(I,J)), AIMAG(W(I,J))))
           DO L = 1, N
              ! W(I,J) = W(I,J) - U(I,L) * V(L,J)
              H = CMPLX(REAL(-REAL(V(L,J)), REAL128), REAL(-AIMAG(V(L,J)), REAL128), REAL128)
              W(I,J) = CMPLX(&
                   IEEE_FMA(REAL(U(I,L)), REAL(H), IEEE_FMA(-AIMAG(U(I,L)), AIMAG(H), REAL(W(I,J)))),&
                   IEEE_FMA(REAL(U(I,L)), AIMAG(H), IEEE_FMA(AIMAG(U(I,L)), REAL(H), AIMAG(W(I,J)))), REAL128)
           END DO
           Y = HYPOT(Y, HYPOT(REAL(W(I,J)), AIMAG(W(I,J))))
        END DO
     END DO
     Y = Y / Z
     IF (ALLOCATED(W)) DEALLOCATE(W)
     IF (ALLOCATED(U)) DEALLOCATE(U)
  END IF
  WRITE (OUTPUT_UNIT,'(ES16.9E2)') Y
  IF (ALLOCATED(SV)) DEALLOCATE(SV)
  IF (ALLOCATED(WRK)) DEALLOCATE(WRK)
  IF (ALLOCATED(V)) DEALLOCATE(V)
  IF (ALLOCATED(G)) DEALLOCATE(G)
CONTAINS
#include "bfopen.f90"
END PROGRAM CJSVDT
