PURE FUNCTION WNRMOA(N, A, LDA, AS)
#ifdef __GFORTRAN__
  USE, INTRINSIC :: ISO_C_BINDING, ONLY: c_long_double
#endif
  USE, INTRINSIC :: ISO_FORTRAN_ENV, ONLY: REAL128
  IMPLICIT NONE
#define HYPOTX HYPOT
  INTEGER, PARAMETER :: KK = REAL128
#ifdef __GFORTRAN__
  INTEGER, PARAMETER :: K = c_long_double
#else
  INTEGER, PARAMETER :: K = REAL128
#endif
  REAL(KIND=KK), PARAMETER :: ZERO = 0.0_KK, SQRT2 = SQRT(2.0_KK)
  INTEGER, INTENT(IN) :: N, LDA, AS
  COMPLEX(KIND=K), INTENT(IN) :: A(LDA,N)
  REAL(KIND=KK) :: WNRMOA
  INTEGER :: I, J, L
  WNRMOA = ZERO
  IF (N .LE. 1) RETURN
  L = -AS
  DO J = 2, N
     DO I = 1, J-1
        WNRMOA = HYPOTX(WNRMOA, HYPOTX(SCALE(REAL(REAL(A(I,J)), KK), L), SCALE(REAL(AIMAG(A(I,J)), KK), L)))
     END DO
  END DO
  WNRMOA = WNRMOA * SQRT2
END FUNCTION WNRMOA
