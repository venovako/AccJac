PURE SUBROUTINE MRTRT(N, A, LDA, P, Q, CS, SN, INFO)
  USE MPFR_F
  IMPLICIT NONE
  INTEGER, INTENT(IN) :: N, LDA, P, Q
  TYPE(MPFR_T), INTENT(INOUT) :: A(LDA,N)
  TYPE(MPFR_T), INTENT(IN) :: CS, SN
  INTEGER, INTENT(INOUT) :: INFO
  TYPE(MPFR_T) :: XX, YY, HS
  INTEGER :: I
  IF (N .LT. 0) INFO = -1
  IF (INFO .LT. 0) RETURN
  IF (IAND(INFO, 4) .EQ. 0) THEN
     CALL MPFR_INIT_M(XX)
     CALL MPFR_INIT_M(YY)
     CALL MPFR_INIT_M(HS)
     CALL MPFR_NEG_F(HS, SN)
     ! SN => TG
     DO I = P+1, Q-1
        CALL MPFR_FMA_F(XX, A(Q,I), SN, A(I,P))
        CALL MPFR_MUL_F(XX, XX, CS)
        CALL MPFR_FMA_F(YY, A(I,P), HS, A(Q,I))
        CALL MPFR_MUL_F(YY, YY, CS)
        A(I,P) = XX
        A(Q,I) = YY
     END DO
     CALL MPFR_FMA_F(XX, A(Q,Q), SN, A(Q,P))
     CALL MPFR_MUL_F(A(Q,P), XX, CS)
     DO I = Q+1, N
        CALL MPFR_FMA_F(XX, A(I,Q), SN, A(I,P))
        CALL MPFR_MUL_F(XX, XX, CS)
        CALL MPFR_FMA_F(YY, A(I,P), HS, A(I,Q))
        CALL MPFR_MUL_F(YY, YY, CS)
        A(I,P) = XX
        A(I,Q) = YY
     END DO
     CALL MPFR_CLEAR_M(HS)
     CALL MPFR_CLEAR_M(YY)
     CALL MPFR_CLEAR_M(XX)
  END IF
END SUBROUTINE MRTRT
