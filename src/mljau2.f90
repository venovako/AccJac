SUBROUTINE MLJAU2(A11, A22, A21, CS, SN, INFO)
  USE MPFR_F
  IMPLICIT NONE
  INTEGER, PARAMETER :: K = REAL64
  REAL(KIND=K), PARAMETER :: ONE = 1.0_K, TWO = 2.0_K
  TYPE(MPFR_T), INTENT(INOUT) :: A11, A22, CS, SN
  TYPE(MPFR_T), INTENT(IN) :: A21
  INTEGER, INTENT(INOUT) :: INFO
  TYPE(MPFR_T) :: AA, T2, T1
  INTEGER(KIND=MPFR_PREC_KIND) :: A21P
  CALL MPFR_INIT_M(AA, A21%MPFR_PREC)
  A21P = MAX(CS%MPFR_PREC, SN%MPFR_PREC)
  CALL MPFR_INIT_M(T2, A21P)
  CALL MPFR_INIT_M(T1, A21P)
  CS = ONE
  CALL MPFR_SET_ZERO(SN, SN%TAG)
  A21P = INT(MPFR_CMP(A21, SN), MPFR_PREC_KIND)
  IF (A21P .EQ. 0_MPFR_PREC_KIND) THEN
     INFO = IOR(INFO, 4)
     RETURN
  END IF
  CALL MPFR_ABS_F(AA, A21)
  IF (A11 .EQ. A22) THEN
     T1 = CS
  ELSE ! the general case
     CALL MPFR_SUB_F(T2, A11, A22)
     CALL MPFR_MUL_D_F(T1, AA, TWO)
     CALL MPFR_DIV_F(T2, T1, T2)
     CALL MPFR_HYPOT_F(T1, T2, CS)
     CALL MPFR_ADD_F(T1, T1, CS)
     CALL MPFR_DIV_F(T1, T2, T1)
  END IF
  CALL MPFR_HYPOT_F(CS, T1, CS)
  CALL MPFR_D_DIV_F(CS, ONE, CS)
  CALL MPFR_NEG_F(SN, T1)
  CALL MPFR_FMA_F(A11, T1, AA, A11)
  CALL MPFR_FMA_F(A22, SN, AA, A22)
  IF (A21P .GT. 0_MPFR_PREC_KIND) CALL MPFR_SET_M(SN, T1)
  CALL MPFR_CLEAR_M(T1)
  CALL MPFR_CLEAR_M(T2)
  CALL MPFR_CLEAR_M(AA)
END SUBROUTINE MLJAU2
