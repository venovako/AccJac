SUBROUTINE DTRKOA(M, N, G, LDG, GS, S, T, U, WRK)
#ifdef __GFORTRAN__
  USE, INTRINSIC :: ISO_C_BINDING, ONLY: c_long_double
  USE, INTRINSIC :: ISO_FORTRAN_ENV, ONLY: REAL64
  IMPLICIT NONE
  INTEGER, PARAMETER :: KK = c_long_double
#else
  USE, INTRINSIC :: ISO_FORTRAN_ENV, ONLY: REAL64, REAL128
  IMPLICIT NONE
  INTEGER, PARAMETER :: KK = REAL128
#endif
  INTEGER, PARAMETER :: K = REAL64
  INTEGER, INTENT(IN) :: M, N, LDG
  REAL(KIND=K), INTENT(IN) :: G(LDG,N)
  INTEGER, INTENT(IN) :: GS, S
  INTEGER, INTENT(INOUT) :: T, U
  REAL(KIND=KK), INTENT(OUT) :: WRK(M,N)
  CHARACTER(LEN=10) :: FN
  INTEGER :: NS, MD, H1
  REAL(KIND=K), EXTERNAL :: DOFFA
  IF ((LDG .LT. M) .OR. (N .LE. 1) .OR. (N .GT. 1000) .OR. (M .LT. N)) RETURN
  IF (S .EQ. 0) THEN
     IF (N .LT. 10) THEN
        WRITE (FN,'(I1)') N
     ELSE IF (N .LT. 100) THEN
        WRITE (FN,'(I2)') N
     ELSE ! N >= 100
        WRITE (FN,'(I3)') N
     END IF
     FN = TRIM(FN)//'doa.csv'
     OPEN(NEWUNIT=U, IOSTAT=T, FILE=TRIM(FN), STATUS='REPLACE', ACTION='WRITE', ACCESS='SEQUENTIAL', FORM='FORMATTED')
     IF (T .EQ. 0) THEN
        WRITE (U,'(A)') '"TRANSF", "SWEEP", "off(A)"'
        FLUSH(U)
     ELSE ! ERROR
        RETURN
     END IF
  END IF
  NS = (N * (N - 1)) / 2
  MD = MOD(T, NS)
  ! HACK: assuming that N/2 is a power of two
  H1 = (N / 2) - 1
  IF ((MD .EQ. 0) .OR. (MD .EQ. 1) .OR. (IAND(MD, H1) .EQ. 0)) THEN
     WRITE (U,'(I11,A,I3,A,ES25.17E3)') T, ',', S, ',', DOFFA(M, N, G, LDG, GS, WRK)
     FLUSH(U)
  END IF
  IF (S .LT. 0) THEN
     CLOSE(U)
  ELSE ! S >= 0
     T = T + 1
  END IF
END SUBROUTINE DTRKOA
