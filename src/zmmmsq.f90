! C += A * B
#ifdef _OPENMP
SUBROUTINE ZMMMSQ(M, N, A, LDA, B, LDB, C, LDC)
#else
PURE SUBROUTINE ZMMMSQ(M, N, A, LDA, B, LDB, C, LDC)
#endif
  USE, INTRINSIC :: ISO_FORTRAN_ENV, ONLY: REAL64
#ifdef USE_IEEE_INTRINSIC
  USE, INTRINSIC :: IEEE_ARITHMETIC, ONLY: IEEE_FMA
#endif
  IMPLICIT NONE
#ifdef USE_IEEE_INTRINSIC
#define DFMA IEEE_FMA
#else
  INTERFACE
     PURE FUNCTION DFMA(A, B, C) BIND(C,NAME='fma')
       USE, INTRINSIC :: ISO_FORTRAN_ENV, ONLY: REAL64
       IMPLICIT NONE
       REAL(KIND=REAL64), INTENT(IN), VALUE :: A, B, C
       REAL(KIND=REAL64) :: DFMA
     END FUNCTION DFMA
  END INTERFACE
#endif
  INTEGER, PARAMETER :: K = REAL64
#define ZFMA(A,B,C) CMPLX(DFMA(REAL(A),REAL(B),DFMA(-AIMAG(A),AIMAG(B),REAL(C))),DFMA(REAL(A),AIMAG(B),DFMA(AIMAG(A),REAL(B),AIMAG(C))),K)
  INTEGER, INTENT(IN) :: M, N, LDA, LDB, LDC
  COMPLEX(KIND=K), INTENT(IN) :: A(LDA,M), B(LDB,N)
  COMPLEX(KIND=K), INTENT(INOUT) :: C(LDC,N)
  INTEGER :: I, J, L
  !$OMP PARALLEL DO DEFAULT(NONE) PRIVATE(I,J,L) SHARED(A,B,C,M,N)
  DO J = 1, N
     DO L = 1, M
        DO I = 1, N
           C(I,J) = ZFMA(A(I,L), B(L,J), C(I,J))
        END DO
     END DO
  END DO
  !$OMP END PARALLEL DO
END SUBROUTINE ZMMMSQ
