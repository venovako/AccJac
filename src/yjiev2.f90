SUBROUTINE YJIEV2(A, B, C, RT1, RT2, CS1, SN1, INFO)
  USE, INTRINSIC :: ISO_C_BINDING, ONLY: c_int
  USE, INTRINSIC :: ISO_FORTRAN_ENV, ONLY: REAL128
  IMPLICIT NONE

  REAL(REAL128), PARAMETER :: ZERO = 0.0_REAL128, ONE = 1.0_REAL128
  COMPLEX(REAL128), INTENT(IN) :: A, B, C
  REAL(REAL128), INTENT(OUT) :: RT1, RT2, CS1
  COMPLEX(REAL128), INTENT(OUT) :: SN1
  INTEGER, INTENT(INOUT) :: INFO
  REAL(REAL128) :: A11, A22, A21R, A21I, SNR, SNI
  INTEGER(c_int) :: ES
  INTEGER(c_int), EXTERNAL :: PVN_YLJEV2

  A11 = REAL(A)
  SNR = AIMAG(A)
  A22 = REAL(C)
  SNI = AIMAG(C)
  A21R = REAL(B)
  A21I = -AIMAG(B)

  ES = INT(INFO, c_int)
  IF (SNR .NE. ZERO) THEN
     INFO = -1
  ELSE IF (SNI .NE. ZERO) THEN
     INFO = -3
  ELSE ! all OK
     INFO = INT(PVN_YLJEV2(A11, A22, A21R, A21I, CS1, SNR, SNI, RT1, RT2, ES))
  END IF

  IF (INFO .LT. 0) THEN
     CS1 = ONE
     SN1 = CMPLX(ZERO, ZERO, REAL128)
     RT1 = ZERO
     RT2 = ZERO
  ELSE ! all OK
     SN1 = CMPLX(SNR, SNI, REAL128)
     IF (ES .NE. 0_c_int) THEN
        RT1 = SCALE(RT1, ES)
        RT2 = SCALE(RT2, ES)
     END IF
  END IF
END SUBROUTINE YJIEV2
