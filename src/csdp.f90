FUNCTION CSDP(M, X, Y, MX, MY, INFO)
  USE, INTRINSIC :: ISO_FORTRAN_ENV, ONLY: REAL32
  IMPLICIT NONE
  INTEGER, PARAMETER :: K = REAL32
  REAL(KIND=K), PARAMETER :: ZERO = 0.0_K
  INTEGER, INTENT(IN) :: M
  COMPLEX(KIND=K), INTENT(IN) :: X(M), Y(M)
  REAL(KIND=K), INTENT(IN) :: MX, MY
  INTEGER, INTENT(OUT) :: INFO
  COMPLEX(KIND=K) :: CSDP, XX, YY
  INTEGER :: I
  INFO = 0
  CSDP = ZERO
  IF (.NOT. (MY .GT. ZERO)) INFO = -5
  IF (.NOT. (MX .GT. ZERO)) INFO = -4
  IF (M .LT. 0) INFO = -1
  IF (INFO .NE. 0) RETURN
  DO I = 1, M
     ! CSDP = CSDP + (CONJG(X(I)) / MX) * (Y(I) / MY)
     XX = CMPLX(REAL(X(I)) / MX, -AIMAG(X(I)) / MX, K)
     YY = CMPLX(REAL(Y(I)) / MY,  AIMAG(Y(I)) / MY, K)
     !DIR$ FMA
     CSDP = CMPLX(&
          (REAL(XX) * REAL(YY) + (REAL(CSDP) - AIMAG(XX) * AIMAG(YY))),&
          (REAL(XX) * AIMAG(YY) + (AIMAG(CSDP) + AIMAG(XX) * REAL(YY))), K)
  END DO
END FUNCTION CSDP
