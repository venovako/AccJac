SUBROUTINE YJAEV2(A, B, C, RT1, RT2, CS1, SN1)
  USE, INTRINSIC :: ISO_C_BINDING, ONLY: c_int
  USE, INTRINSIC :: ISO_FORTRAN_ENV, ONLY: REAL128
  IMPLICIT NONE

  INTERFACE
     FUNCTION YLJEV2(A11, A22, A21R, A21I, CS, SNR, SNI, L1, L2, ES) BIND(C,NAME='pvn_yljev2_')
       USE, INTRINSIC :: ISO_C_BINDING, ONLY: c_int
       USE, INTRINSIC :: ISO_FORTRAN_ENV, ONLY: REAL128
       IMPLICIT NONE
       REAL(REAL128), INTENT(IN), TARGET :: A11, A22, A21R, A21I
       REAL(REAL128), INTENT(OUT), TARGET :: CS, SNR, SNI, L1, L2
       INTEGER(c_int), INTENT(INOUT), TARGET :: ES
       INTEGER(c_int) :: YLJEV2
     END FUNCTION YLJEV2
  END INTERFACE

  REAL(REAL128), PARAMETER :: ZERO = 0.0_REAL128, ONE = 1.0_REAL128
  COMPLEX(REAL128), INTENT(IN) :: A, B, C
  REAL(REAL128), INTENT(OUT), TARGET :: RT1, RT2, CS1
  COMPLEX(REAL128), INTENT(OUT) :: SN1
  REAL(REAL128), TARGET :: A11, A22, A21R, A21I, SNR, SNI
  INTEGER(c_int), TARGET :: ES

  A11 = REAL(A)
  SNR = AIMAG(A)
  A22 = REAL(C)
  SNI = AIMAG(C)
  A21R = REAL(B)
  A21I = -AIMAG(B)
  ES = 0_c_int

  IF ((SNR .NE. ZERO) .OR. (SNI .NE. ZERO) .OR. (YLJEV2(A11, A22, A21R, A21I, CS1, SNR, SNI, RT1, RT2, ES) .LT. 0_c_int)) THEN
     CS1 = ONE
     SN1 = CMPLX(ZERO, ZERO, REAL128)
     RT1 = ZERO
     RT2 = ZERO
  ELSE ! all OK
     SN1 = CMPLX(SNR, SNI, REAL128)
     IF (ES .NE. 0_c_int) THEN
        RT1 = SCALE(RT1, ES)
        RT2 = SCALE(RT2, ES)
     END IF
  END IF
END SUBROUTINE YJAEV2
