PURE FUNCTION ZNRMOA(N, A, LDA, AS)
  USE, INTRINSIC :: ISO_C_BINDING, ONLY: c_long_double
  USE, INTRINSIC :: ISO_FORTRAN_ENV, ONLY: REAL64, REAL128
  IMPLICIT NONE
#ifdef USE_IEEE_INTRINSIC
#define XSQRT SQRT
#else
  INTERFACE
#ifdef __GFORTRAN__
     PURE FUNCTION XSQRT(X) BIND(C,NAME='sqrtl')
       USE, INTRINSIC :: ISO_C_BINDING, ONLY: c_long_double
#else
     PURE FUNCTION XSQRT(X) BIND(C,NAME='cr_sqrtq')
       USE, INTRINSIC :: ISO_FORTRAN_ENV, ONLY: REAL128
#endif
       IMPLICIT NONE
#ifdef __GFORTRAN__
       REAL(KIND=c_long_double), INTENT(IN), VALUE :: X
       REAL(KIND=c_long_double) :: XSQRT
#else
       REAL(KIND=REAL128), INTENT(IN), VALUE :: X
       REAL(KIND=REAL128) :: XSQRT
#endif
     END FUNCTION XSQRT
  END INTERFACE
#endif
#ifdef __GFC_REAL_10__
  INTERFACE
     PURE FUNCTION CR_HYPOT(X, Y) BIND(C,NAME='cr_hypotl')
       USE, INTRINSIC :: ISO_C_BINDING, ONLY: c_long_double
       IMPLICIT NONE
       REAL(KIND=c_long_double), INTENT(IN), VALUE :: X, Y
       REAL(KIND=c_long_double) :: CR_HYPOT
     END FUNCTION CR_HYPOT
  END INTERFACE
  INTEGER, PARAMETER :: KK = c_long_double
#else
  INTERFACE
     PURE FUNCTION CR_HYPOT(X, Y) BIND(C,NAME='cr_hypotq')
       USE, INTRINSIC :: ISO_FORTRAN_ENV, ONLY: REAL128
       IMPLICIT NONE
       REAL(KIND=REAL128), INTENT(IN), VALUE :: X, Y
       REAL(KIND=REAL128) :: CR_HYPOT
     END FUNCTION CR_HYPOT
  END INTERFACE
  INTEGER, PARAMETER :: KK = REAL128
#endif
  INTEGER, PARAMETER :: K = REAL64
  REAL(KIND=KK), PARAMETER :: ZERO = 0.0_KK
  INTEGER, INTENT(IN) :: N, LDA, AS
  COMPLEX(KIND=K), INTENT(IN) :: A(LDA,N)
  REAL(KIND=KK) :: ZNRMOA
  INTEGER :: I, J, L
  ZNRMOA = ZERO
  IF (N .LE. 1) RETURN
  L = -AS
  DO J = 1, N-1
     DO I = J+1, N
        ZNRMOA = CR_HYPOT(ZNRMOA, CR_HYPOT(SCALE(REAL(REAL(A(I,J)), KK), L), SCALE(REAL(AIMAG(A(I,J)), KK), L)))
     END DO
  END DO
  ZNRMOA = ZNRMOA * XSQRT(2.0_KK)
END FUNCTION ZNRMOA
