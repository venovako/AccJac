SUBROUTINE ZTRCOA(N, A, LDA, AS, S, T, U)
  USE, INTRINSIC :: ISO_FORTRAN_ENV, ONLY: REAL64
  IMPLICIT NONE
  INTERFACE
     PURE FUNCTION ZNRMOA(N, A, LDA, AS)
#ifdef __GFORTRAN__
       USE, INTRINSIC :: ISO_C_BINDING, ONLY: c_long_double
       USE, INTRINSIC :: ISO_FORTRAN_ENV, ONLY: REAL64
#else
       USE, INTRINSIC :: ISO_FORTRAN_ENV, ONLY: REAL64, REAL128
#endif
       IMPLICIT NONE
       INTEGER, INTENT(IN) :: N, LDA, AS
       COMPLEX(KIND=REAL64), INTENT(IN) :: A(LDA,N)
#ifdef __GFORTRAN__
       REAL(KIND=c_long_double) :: ZNRMOA
#else
       REAL(KIND=REAL128) :: ZNRMOA
#endif
     END FUNCTION ZNRMOA
  END INTERFACE
  INTEGER, PARAMETER :: K = REAL64
  INTEGER, INTENT(IN) :: N, LDA, AS, S
  COMPLEX(KIND=K), INTENT(IN) :: A(LDA,N)
  INTEGER, INTENT(INOUT) :: T, U
  CHARACTER(LEN=10) :: FN
  INTEGER :: NS, MD, H1

  IF ((LDA .LT. N) .OR. (N .LE. 1) .OR. (N .GE. 1000)) RETURN
  IF (S .EQ. 0) THEN
     IF (N .LT. 10) THEN
        WRITE (FN,'(I1)') N
     ELSE IF (N .LT. 100) THEN
        WRITE (FN,'(I2)') N
     ELSE ! N >= 100
        WRITE (FN,'(I3)') N
     END IF
     FN = TRIM(FN)//'ZOA.csv'
     OPEN(NEWUNIT=U, IOSTAT=T, FILE=TRIM(FN), STATUS='REPLACE', ACTION='WRITE', ACCESS='SEQUENTIAL', FORM='FORMATTED')
     IF (T .EQ. 0) THEN
        WRITE (U,'(A)') '"TRANSF", "SWEEP", "off(A)"'
        FLUSH(U)
     ELSE ! ERROR
        RETURN
     END IF
  END IF
  NS = (N * (N - 1)) / 2
  MD = MOD(T, NS)
  H1 = N / 2
  IF ((MD .EQ. 0) .OR. (MD .EQ. 1) .OR. (MOD(MD, H1) .EQ. 0)) THEN
     WRITE (U,'(I11,A,I3,A,ES25.17E3)') T, ',', S, ',', ZNRMOA(N, A, LDA, AS)
     FLUSH(U)
  END IF
  IF (S .LT. 0) THEN
     CLOSE(U)
  ELSE ! S >= 0
     T = T + 1
  END IF
END SUBROUTINE ZTRCOA
