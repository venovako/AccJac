PROGRAM DJSVOR
#ifdef __GFORTRAN__
  USE, INTRINSIC :: ISO_C_BINDING, ONLY: c_long_double
  USE, INTRINSIC :: ISO_FORTRAN_ENV, ONLY: INT64, OUTPUT_UNIT, REAL64
#else
  USE, INTRINSIC :: ISO_FORTRAN_ENV, ONLY: INT64, OUTPUT_UNIT, REAL64, REAL128
#endif
  IMPLICIT NONE
  INTERFACE
#ifdef _OPENMP
     SUBROUTINE XMMMSQ(M, N, A, LDA, B, LDB, C, LDC)
#else
     PURE SUBROUTINE XMMMSQ(M, N, A, LDA, B, LDB, C, LDC)
#endif
#ifdef __GFORTRAN__
       USE, INTRINSIC :: ISO_C_BINDING, ONLY: c_long_double
#else
       USE, INTRINSIC :: ISO_FORTRAN_ENV, ONLY: REAL128
#endif
       IMPLICIT NONE
       INTEGER, INTENT(IN) :: M, N, LDA, LDB, LDC
#ifdef __GFORTRAN__
       REAL(KIND=c_long_double), INTENT(IN) :: A(LDA,M), B(LDB,N)
       REAL(KIND=c_long_double), INTENT(INOUT) :: C(LDC,N)
#else
       REAL(KIND=REAL128), INTENT(IN) :: A(LDA,M), B(LDB,N)
       REAL(KIND=REAL128), INTENT(INOUT) :: C(LDC,N)
#endif
     END SUBROUTINE XMMMSQ
  END INTERFACE
#ifdef __GFORTRAN__
  INTERFACE
     PURE FUNCTION CR_HYPOT(X, Y) BIND(C,NAME='cr_hypotl')
       USE, INTRINSIC :: ISO_C_BINDING, ONLY: c_long_double
       IMPLICIT NONE
       REAL(KIND=c_long_double), INTENT(IN), VALUE :: X, Y
       REAL(KIND=c_long_double) :: CR_HYPOT
     END FUNCTION CR_HYPOT
  END INTERFACE
  INTEGER, PARAMETER :: KK = c_long_double
#else
#define CR_HYPOT HYPOT
  INTEGER, PARAMETER :: KK = REAL128
#endif
  INTEGER, PARAMETER :: K = REAL64
  REAL(KIND=KK), PARAMETER :: XZERO = 0.0_KK, XMONE = -1.0_KK, XONE = 1.0_KK
  CHARACTER(LEN=256) :: CLA
  INTEGER(KIND=INT64), ALLOCATABLE :: JV(:)
  REAL(KIND=K), ALLOCATABLE :: V(:,:)
  REAL(KIND=KK), ALLOCATABLE :: XV(:,:), XH(:,:), XU(:,:)
  REAL(KIND=KK) :: X
  INTEGER :: M, N, I, J, L, JPOS
  EXTERNAL :: BFOPEN
  !$OMP DECLARE REDUCTION(HYP:REAL(KK):OMP_OUT=CR_HYPOT(OMP_OUT,OMP_IN)) INITIALIZER(OMP_PRIV=XZERO)
  I = COMMAND_ARGUMENT_COUNT()
  IF (I .NE. 3) STOP 'djsvor.exe N M|JPOS FILENAME'
  CALL GET_COMMAND_ARGUMENT(1, CLA)
  READ (CLA,*) N
  IF (N .LE. 0) STOP 'N'
  CALL GET_COMMAND_ARGUMENT(2, CLA)
  READ (CLA,*) JPOS
  IF (JPOS .LT. -1) STOP 'JPOS'
  CALL GET_COMMAND_ARGUMENT(3, CLA)
  M = MAX(JPOS, N)
  CALL BFOPEN(TRIM(CLA), 'RO', I, J)
  IF (J .NE. 0) STOP 'OPEN'
  ALLOCATE(V(M,N))
  READ (UNIT=I, IOSTAT=J) V
  IF (J .NE. 0) STOP 'READ'
  CLOSE (UNIT=I, IOSTAT=J)
  IF (J .NE. 0) STOP 'CLOSE'
  IF (JPOS .EQ. -1) THEN
     ALLOCATE(JV(N))
     CALL BFOPEN(TRIM(CLA)//'.J', 'RO', I, J)
     IF (J .NE. 0) STOP 'OPEN(J)'
     READ (UNIT=I, IOSTAT=J) JV
     IF (J .NE. 0) STOP 'READ(J)'
     CLOSE (UNIT=I, IOSTAT=J)
     IF (J .NE. 0) STOP 'CLOSE(J)'
     JPOS = 0
     J = 1
     DO WHILE (JV(J) .EQ. 1_INT64)
        JPOS = JPOS + 1
        J = J + 1
        IF (J .GT. N) EXIT
     END DO
     IF (J .LE. N) THEN
        DO WHILE (JV(J) .EQ. -1_INT64)
           J = J + 1
           IF (J .GT. N) EXIT
        END DO
        IF (J .LE. N) STOP 'J not [+I,-I]'
     END IF
     DEALLOCATE(JV)
  END IF
  L = MIN(JPOS, N)
  ALLOCATE(XV(M,N))
  ALLOCATE(XH(N,M))
  ALLOCATE(XU(N,N))
  ! J*V
  !$OMP PARALLEL DO DEFAULT(NONE) PRIVATE(I,J) SHARED(V,XV,JPOS,M,N)
  DO J = 1, N
     DO I = 1, JPOS
        XV(I,J) = V(I,J)
     END DO
     DO I = JPOS+1, M
        XV(I,J) = -V(I,J)
     END DO
  END DO
  !$OMP END PARALLEL DO
  ! V^T
  !$OMP PARALLEL DO DEFAULT(NONE) PRIVATE(I,J) SHARED(V,XH,M,N)
  DO J = 1, M
     DO I = 1, N
        XH(I,J) = V(J,I)
     END DO
  END DO
  !$OMP END PARALLEL DO
  ! -J
  !$OMP PARALLEL DO DEFAULT(NONE) PRIVATE(I,J) SHARED(XU,L,N)
  DO J = 1, L
     DO I = 1, J-1
        XU(I,J) = XZERO
     END DO
     XU(J,J) = XMONE
     DO I = J+1, N
        XU(I,J) = XZERO
     END DO
  END DO
  !$OMP END PARALLEL DO
  !$OMP PARALLEL DO DEFAULT(NONE) PRIVATE(I,J) SHARED(XU,L,N)
  DO J = L+1, N
     DO I = 1, J-1
        XU(I,J) = XZERO
     END DO
     XU(J,J) = XONE
     DO I = J+1, N
        XU(I,J) = XZERO
     END DO
  END DO
  !$OMP END PARALLEL DO
  CALL XMMMSQ(M, N, XH, N, XV, M, XU, N)
  X = XZERO
  !$OMP PARALLEL DO DEFAULT(NONE) PRIVATE(I,J) SHARED(XU,N) REDUCTION(HYP:X)
  DO J = 1, N
     DO I = 1, N
        X = CR_HYPOT(X, XU(I,J))
     END DO
  END DO
  !$OMP END PARALLEL DO
  WRITE (OUTPUT_UNIT,'(ES25.17E3)') REAL(X, K)
  FLUSH(OUTPUT_UNIT)
  DEALLOCATE(XU)
  DEALLOCATE(XH)
  DEALLOCATE(XV)
  DEALLOCATE(V)
END PROGRAM DJSVOR
