PROGRAM DJEVDT
#ifdef __GFORTRAN__
  USE, INTRINSIC :: ISO_C_BINDING, ONLY: c_long_double
  USE, INTRINSIC :: ISO_FORTRAN_ENV, ONLY: INT64, OUTPUT_UNIT, REAL64
#else
  USE, INTRINSIC :: ISO_FORTRAN_ENV, ONLY: INT64, OUTPUT_UNIT, REAL64, REAL128
#endif
  IMPLICIT NONE
#ifdef __GFORTRAN__
  INTEGER, PARAMETER :: KK = c_long_double
#else
  INTEGER, PARAMETER :: KK = REAL128
#endif
  INTEGER, PARAMETER :: K = REAL64
  CHARACTER(LEN=256) :: CLA
  REAL(KIND=KK) :: X
  INTEGER :: N, JPOS, INFO, I, J, AS
  REAL(KIND=K), ALLOCATABLE :: A(:,:), V(:,:), WRK(:,:)
  EXTERNAL :: BFOPEN, DJEVDR
  ! read the command line arguments
  I = COMMAND_ARGUMENT_COUNT()
  IF (I .NE. 4) STOP 'djevdt.exe N JPOS OPTS FILE'
  I = 0
  CALL GET_COMMAND_ARGUMENT(1, CLA)
  READ (CLA,*) N
  IF (N .LT. 0) THEN
     N = -N
     I = 4
  ELSE IF (N .EQ. 0) THEN
     STOP 'N'
  END IF
  CALL GET_COMMAND_ARGUMENT(2, CLA)
  READ (CLA,*) JPOS
  IF ((JPOS .LT. 0) .OR. (JPOS .GT. N)) STOP 'JPOS'
  CALL GET_COMMAND_ARGUMENT(3, CLA)
  READ (CLA,*) INFO
  IF ((INFO .LT. 0) .OR. (INFO .GT. 3)) STOP 'OPTS'
  INFO = IOR(INFO, I)
  CALL GET_COMMAND_ARGUMENT(4, CLA)
  IF (LEN_TRIM(CLA) .LE. 0) STOP 'FILE'
  CALL BFOPEN(TRIM(CLA)//'.A', 'RO', I, J)
  IF (J .NE. 0) STOP 'OPEN(A)'
  ALLOCATE(A(N,N))
  READ (UNIT=I, IOSTAT=J) A
  IF (J .NE. 0) STOP 'READ(A)'
  CLOSE (UNIT=I, IOSTAT=J)
  IF (J .NE. 0) STOP 'CLOSE(A)'
  ALLOCATE(V(N,N))
  ALLOCATE(WRK(N,N))
  AS = HUGE(AS)
  CALL DJEVDR(N, A, N, V, N, JPOS, WRK, AS, INFO)
  WRITE (OUTPUT_UNIT,'(I2,A,I6,A,I11)') INFO, ',', AS, ',', INT(WRK(1,1), INT64)
  FLUSH(OUTPUT_UNIT)
  DEALLOCATE(WRK)
  CALL BFOPEN(TRIM(CLA)//'.V', 'WO', I, J)
  IF (J .NE. 0) STOP 'OPEN(V)'
  WRITE (UNIT=I, IOSTAT=J) V
  IF (J .NE. 0) STOP 'WRITE(V)'
  CLOSE (UNIT=I, IOSTAT=J)
  IF (J .NE. 0) STOP 'CLOSE(V)'
  DEALLOCATE(V)
  OPEN(NEWUNIT=INFO, FILE=TRIM(CLA)//'.AE', STATUS='REPLACE', ACTION='WRITE', ACCESS='SEQUENTIAL', FORM='FORMATTED')
  I = -AS
  DO J = 1, JPOS
     X = A(J,J)
     A(J,J) = SCALE( A(J,J), I)
     X = SCALE(X, I)
     WRITE (INFO,'(ES25.17E3)') X
  END DO
  DO J = JPOS+1, N
     X = -A(J,J)
     A(J,J) = SCALE(-A(J,J), I)
     X = SCALE(X, I)
     WRITE (INFO,'(ES25.17E3)') X
  END DO
  CLOSE(INFO)
  CALL BFOPEN(TRIM(CLA)//'.L', 'WO', I, J)
  IF (J .NE. 0) STOP 'OPEN(L)'
  DO INFO = 1, JPOS
     WRITE (UNIT=I, IOSTAT=J) A(INFO,INFO)
     IF (J .NE. 0) STOP 'WRITE(L+)'
  END DO
  DO INFO = JPOS+1, N
     WRITE (UNIT=I, IOSTAT=J) A(INFO,INFO)
     IF (J .NE. 0) STOP 'WRITE(L-)'
  END DO
  CLOSE (UNIT=I, IOSTAT=J)
  IF (J .NE. 0) STOP 'CLOSE(L)'
  DEALLOCATE(A)
END PROGRAM DJEVDT
