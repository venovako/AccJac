PROGRAM DJEVDT
#ifdef __GFORTRAN__
  USE, INTRINSIC :: ISO_C_BINDING, ONLY: c_long_double
  USE, INTRINSIC :: ISO_FORTRAN_ENV, ONLY: INT64, OUTPUT_UNIT, REAL64
#else
  USE, INTRINSIC :: ISO_FORTRAN_ENV, ONLY: INT64, OUTPUT_UNIT, REAL64, REAL128
#endif
  IMPLICIT NONE
#ifdef __GFORTRAN__
  INTERFACE
     PURE FUNCTION HYPOTX(X, Y) BIND(C,NAME='cr_hypotl')
       USE, INTRINSIC :: ISO_C_BINDING, ONLY: c_long_double
       IMPLICIT NONE
       REAL(KIND=c_long_double), INTENT(IN), VALUE :: X, Y
       REAL(KIND=c_long_double) :: HYPOTX
     END FUNCTION HYPOTX
  END INTERFACE
  INTEGER, PARAMETER :: KK = c_long_double
#else
#define HYPOTX HYPOT
  INTEGER, PARAMETER :: KK = REAL128
#endif
  INTEGER, PARAMETER :: K = REAL64
  REAL(KIND=KK), PARAMETER :: XZERO = 0.0_KK
  REAL(KIND=K), PARAMETER :: ZERO = 0.0_K
  CHARACTER(LEN=256) :: CLA
  INTEGER :: N, JPOS, INFO, I, J, AS
  REAL(KIND=K), ALLOCATABLE :: A(:,:), V(:,:), WRK(:,:)
  REAL(KIND=KK), ALLOCATABLE :: X(:,:), Y(:,:), Z(:,:)
  REAL(KIND=K), EXTERNAL :: DNRMF
  EXTERNAL :: BFOPEN, DJEVDR
  ! read the command line arguments
  I = COMMAND_ARGUMENT_COUNT()
  IF (I .NE. 4) STOP 'djevdt.exe N JPOS OPTS FILE'
  I = 0
  CALL GET_COMMAND_ARGUMENT(1, CLA)
  READ (CLA,*) N
  IF (N .LT. 0) THEN
     N = -N
     I = 4
  ELSE IF (N .EQ. 0) THEN
     STOP 'N'
  END IF
  CALL GET_COMMAND_ARGUMENT(2, CLA)
  READ (CLA,*) JPOS
  IF ((JPOS .LT. 0) .OR. (JPOS .GT. N)) STOP 'JPOS'
  CALL GET_COMMAND_ARGUMENT(3, CLA)
  READ (CLA,*) INFO
  IF ((INFO .LT. 0) .OR. (INFO .GT. 3)) STOP 'OPTS'
  INFO = IOR(INFO, I)
  CALL GET_COMMAND_ARGUMENT(4, CLA)
  IF (LEN_TRIM(CLA) .LE. 0) STOP 'FILE'
  CALL BFOPEN(TRIM(CLA)//'.A', 'RO', I, J)
  IF (J .NE. 0) STOP 'OPEN(A)'
  ALLOCATE(A(N,N))
  READ (UNIT=I, IOSTAT=J) A
  IF (J .NE. 0) STOP 'READ(A)'
  CLOSE (UNIT=I, IOSTAT=J)
  IF (J .NE. 0) STOP 'CLOSE(A)'
  ALLOCATE(V(N,N))
  ALLOCATE(WRK(N,N))
  AS = HUGE(AS)
  CALL DJEVDR(N, A, N, V, N, JPOS, WRK, AS, INFO)
  WRITE (OUTPUT_UNIT,'(I2,A,I6,A,I11,A)',ADVANCE='NO') INFO, ',', AS, ',', INT(WRK(1,1), INT64), ','
  FLUSH(OUTPUT_UNIT)
  CALL BFOPEN(TRIM(CLA)//'.V', 'WO', I, J)
  IF (J .NE. 0) STOP 'OPEN(V)'
  WRITE (UNIT=I, IOSTAT=J) V
  IF (J .NE. 0) STOP 'WRITE(V)'
  CLOSE (UNIT=I, IOSTAT=J)
  IF (J .NE. 0) STOP 'CLOSE(V)'
  ALLOCATE(X(N,N))
  ALLOCATE(Y(N,N))
  ALLOCATE(Z(N,N))
  INFO = -AS
  DO J = 1, N
     DO I = 1, J-1
        A(I,J) = ZERO
     END DO
     Z(J,J) = A(J,J)
     A(J,J) = SCALE(A(J,J), INFO)
     Z(J,J) = SCALE(Z(J,J), INFO)
     DO I = J+1, N
        A(I,J) = ZERO
     END DO
  END DO
  ! V^-T = WRK := J V J
  DO J = 1, JPOS
     DO I = 1, JPOS
        WRK(I,J) = V(I,J)
     END DO
     DO I = JPOS+1, N
        WRK(I,J) = -V(I,J)
     END DO
  END DO
  DO J = JPOS+1, N
     DO I = 1, JPOS
        WRK(I,J) = -V(I,J)
     END DO
     DO I = JPOS+1, N
        WRK(I,J) = V(I,J)
     END DO
  END DO
  ! Y := V^-T D
  DO J = 1, N
     DO I = 1, N
        Y(I,J) = WRK(I,J) * Z(J,J)
     END DO
  END DO
  ! Z := V^-1 = (V^-1)^T = WRK^T
  DO J = 1, N
     DO I = 1, N
        Z(I,J) = WRK(J,I)
     END DO
  END DO
  ! V^T A V = D ==> A = V^-T D V^-1
  X = MATMUL(Y, Z)
  CALL BFOPEN(TRIM(CLA)//'.A', 'RO', I, J)
  IF (J .NE. 0) STOP 'OPEN(A)'
  READ (UNIT=I, IOSTAT=J) A
  IF (J .NE. 0) STOP 'READ(A)'
  CLOSE (UNIT=I, IOSTAT=J)
  IF (J .NE. 0) STOP 'CLOSE(A)'
  Z(1,1) = XZERO
  DO J = 1, N
     DO I = 1, N
        Y(I,J) = A(I,J) - X(I,J)
        Z(1,1) = HYPOTX(Z(1,1), Y(I,J))
     END DO
  END DO
  INFO = N * N
  IF (Z(1,1) .NE. XZERO) Z(1,1) = Z(1,1) / DNRMF(INFO, A)
  WRITE (OUTPUT_UNIT,'(ES25.17E3)') Z(1,1)
  DEALLOCATE(Z)
  DEALLOCATE(Y)
  DEALLOCATE(X)
  DEALLOCATE(WRK)
  DEALLOCATE(V)
  DEALLOCATE(A)
END PROGRAM DJEVDT
