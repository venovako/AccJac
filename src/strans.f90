! IN: INFO & 1: sin => tan
!     INFO & 2: hyp
!     INFO & 4: JPOS < P < Q
!OUT: INFO = 0: notransf (convergence criterion)
!     INFO = 1: transf (but identity, so no-op)
!     INFO = 2: transf, no downscaling of G and SV
!     INFO = 3: transf with downscaling of G and SV
SUBROUTINE STRANS(M, N, G, LDG, V, LDV, SV, GX, GS, P, Q, TOL, INFO)
  USE, INTRINSIC :: ISO_FORTRAN_ENV, ONLY: REAL32
  IMPLICIT NONE
  INTERFACE
     FUNCTION SSDP(M, X, Y, MX, MY, INFO)
       USE, INTRINSIC :: ISO_FORTRAN_ENV, ONLY: REAL32
       IMPLICIT NONE
       INTEGER, INTENT(IN) :: M
       REAL(KIND=REAL32), INTENT(IN) :: X(M), Y(M), MX, MY
       INTEGER, INTENT(OUT) :: INFO
       REAL(KIND=REAL32) :: SSDP
     END FUNCTION SSDP
  END INTERFACE
  INTERFACE
     PURE SUBROUTINE SGRAM(PNF, QNF, QPS, APP, AQQ, AQP, INFO)
       USE, INTRINSIC :: ISO_FORTRAN_ENV, ONLY: REAL32
       IMPLICIT NONE
       REAL(KIND=REAL32), INTENT(IN) :: PNF, QNF, QPS
       REAL(KIND=REAL32), INTENT(OUT) :: APP, AQQ, AQP
       INTEGER, INTENT(OUT) :: INFO
     END SUBROUTINE SGRAM
  END INTERFACE
  INTERFACE
     SUBROUTINE SLJU2(A11, A22, A21, CS, SN, INFO)
       USE, INTRINSIC :: ISO_FORTRAN_ENV, ONLY: REAL32
       IMPLICIT NONE
       REAL(KIND=REAL32), INTENT(IN) :: A11, A22, A21
       REAL(KIND=REAL32), INTENT(OUT) :: CS, SN
       INTEGER, INTENT(INOUT) :: INFO
     END SUBROUTINE SLJU2
  END INTERFACE
  INTERFACE
     SUBROUTINE SLJV2(A11, A22, A21, CH, SH, INFO)
       USE, INTRINSIC :: ISO_FORTRAN_ENV, ONLY: REAL32
       IMPLICIT NONE
       REAL(KIND=REAL32), INTENT(IN) :: A11, A22, A21
       REAL(KIND=REAL32), INTENT(OUT) :: CH, SH
       INTEGER, INTENT(INOUT) :: INFO
     END SUBROUTINE SLJV2
  END INTERFACE
  INTERFACE
     PURE SUBROUTINE SRTVT(M, X, Y, CS, SN, INFO)
       USE, INTRINSIC :: ISO_FORTRAN_ENV, ONLY: REAL32
       IMPLICIT NONE
       INTEGER, INTENT(IN) :: M
       REAL(KIND=REAL32), INTENT(INOUT) :: X(M), Y(M)
       REAL(KIND=REAL32), INTENT(IN) :: CS, SN
       INTEGER, INTENT(INOUT) :: INFO
     END SUBROUTINE SRTVT
  END INTERFACE
  INTERFACE
     PURE SUBROUTINE SRTVH(M, X, Y, CH, SH, INFO)
       USE, INTRINSIC :: ISO_FORTRAN_ENV, ONLY: REAL32
       IMPLICIT NONE
       INTEGER, INTENT(IN) :: M
       REAL(KIND=REAL32), INTENT(INOUT) :: X(M), Y(M)
       REAL(KIND=REAL32), INTENT(IN) :: CH, SH
       INTEGER, INTENT(INOUT) :: INFO
     END SUBROUTINE SRTVH
  END INTERFACE
  INTERFACE
     PURE SUBROUTINE SROTT(M, X, Y, CS, SN, GX, MX, MY, INFO)
       USE, INTRINSIC :: ISO_FORTRAN_ENV, ONLY: REAL32
       IMPLICIT NONE
       INTEGER, INTENT(IN) :: M
       REAL(KIND=REAL32), INTENT(INOUT) :: X(M), Y(M), GX, MX, MY
       REAL(KIND=REAL32), INTENT(IN) :: CS, SN
       INTEGER, INTENT(INOUT) :: INFO
     END SUBROUTINE SROTT
  END INTERFACE
  INTERFACE
     PURE SUBROUTINE SROTH(M, X, Y, CH, SH, GX, MX, MY, INFO)
       USE, INTRINSIC :: ISO_FORTRAN_ENV, ONLY: REAL32
       IMPLICIT NONE
       INTEGER, INTENT(IN) :: M
       REAL(KIND=REAL32), INTENT(INOUT) :: X(M), Y(M), GX, MX, MY
       REAL(KIND=REAL32), INTENT(IN) :: CH, SH
       INTEGER, INTENT(INOUT) :: INFO
     END SUBROUTINE SROTH
  END INTERFACE
  INTERFACE
     PURE SUBROUTINE SSCALG(M, N, G, LDG, GX, GS, INFO)
       USE, INTRINSIC :: ISO_FORTRAN_ENV, ONLY: REAL32
       IMPLICIT NONE
       INTEGER, INTENT(IN) :: M, N, LDG
       REAL(KIND=REAL32), INTENT(INOUT) :: G(LDG,N), GX
       INTEGER, INTENT(INOUT) :: GS, INFO
     END SUBROUTINE SSCALG
  END INTERFACE
  INTEGER, PARAMETER :: K = REAL32
  REAL(KIND=K), PARAMETER :: ZERO = 0.0_K
  INTEGER, INTENT(IN) :: M, N, LDG, LDV, P, Q
  REAL(KIND=K), INTENT(INOUT) :: G(LDG,N), V(LDV,N), SV(N), GX
  REAL(KIND=K), INTENT(IN) :: TOL
  INTEGER, INTENT(INOUT) :: GS, INFO
  REAL(KIND=K) :: QPS, APP, AQQ, AQP, C, S, T
  INTEGER :: I, J, L
  IF ((INFO .LT. 0) .OR. (INFO .GT. 7)) INFO = -13
  IF (TOL .LT. ZERO) INFO = -12
  IF ((Q .LE. P) .OR. (Q .GT. N)) INFO = -11
  IF ((P .LE. 0) .OR. (P .GT. N)) INFO = -10
  IF (LDV .LT. N) INFO = -6
  IF (LDG .LT. M) INFO = -4
  IF ((N .LT. 0) .OR. (N .GT. M)) INFO = -2
  IF (M .LT. 0) INFO = -1
  IF (INFO .LT. 0) RETURN
  IF (M .EQ. 0) RETURN
  I = 0
  QPS = SSDP(M, G(1,Q), G(1,P), SV(Q), SV(P), I)
  IF (I .NE. 0) THEN
     INFO = -3
     RETURN
  END IF
  T = ABS(QPS)
  IF (.NOT. (T .LE. HUGE(T))) THEN
     INFO = -3
     RETURN
  END IF
  L = INFO
  IF (T .LT. TOL) THEN
     INFO = 0
     RETURN
  ELSE ! transform
     INFO = 2
  END IF
  J = 0
  CALL SGRAM(SV(P), SV(Q), QPS, APP, AQQ, AQP, J)
  IF (J .LE. -HUGE(J)) THEN
     INFO = -7
     RETURN
  END IF
  J = IAND(L, 2)
  I = IAND(L, 1)
  IF (IAND(L, 4) .NE. 0) I = IOR(I, 16)
  T = GX
  IF (J .EQ. 0) THEN
     CALL SLJU2(APP, AQQ, AQP, C, S, I)
     CALL SROTT(M, G(1,P), G(1,Q), C, S, T, SV(P), SV(Q), I)
     CALL SRTVT(N, V(1,P), V(1,Q), C, S, I)
  ELSE ! hyp
     CALL SLJV2(APP, AQQ, AQP, C, S, I)
     CALL SROTH(M, G(1,P), G(1,Q), C, S, T, SV(P), SV(Q), I)
     CALL SRTVH(N, V(1,P), V(1,Q), C, S, I)
  END IF
  IF (I .LT. 0) THEN
     INFO = -8
  ELSE IF ((IAND(I, 4) .NE. 0) .AND. (IAND(I, 8) .EQ. 0)) THEN
     INFO = 1
  ELSE IF (T .GT. GX) THEN
     GX = T
     I = 1
     CALL SSCALG(M, N, G, LDG, GX, GS, I)
     IF (I .LT. 0) THEN
        INFO = -9
        RETURN
     END IF
     IF (I .GT. 0) THEN
        I = -I
        DO J = 1, N
           SV(J) = SCALE(SV(J), I)
        END DO
        INFO = 3
     END IF
  END IF
END SUBROUTINE STRANS
