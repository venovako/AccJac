FUNCTION CNRMF(M, X, INFO)
  USE, INTRINSIC :: ISO_FORTRAN_ENV, ONLY: REAL32
  IMPLICIT NONE
  INTERFACE
     PURE FUNCTION CR_HYPOT(X, Y) BIND(C,NAME='cr_hypotf')
       USE, INTRINSIC :: ISO_C_BINDING, ONLY: c_float
       IMPLICIT NONE
       REAL(KIND=c_float), INTENT(IN), VALUE :: X, Y
       REAL(KIND=c_float) :: CR_HYPOT
     END FUNCTION CR_HYPOT
  END INTERFACE
  INTEGER, PARAMETER :: K = REAL32
  REAL(KIND=K), PARAMETER :: ZERO = 0.0_K
  INTEGER, INTENT(IN) :: M
  COMPLEX(KIND=K), INTENT(IN) :: X(M)
  INTEGER, INTENT(OUT) :: INFO
  REAL(KIND=K) :: CNRMF
  INTEGER :: I
  INFO = 0
  CNRMF = ZERO
  IF (M .LT. 0) INFO = -1
  IF (INFO .NE. 0) RETURN
  IF (M .EQ. 0) RETURN
  CNRMF = CR_HYPOT(REAL(X(1)), AIMAG(X(1)))
  IF (.NOT. (CNRMF .LE. HUGE(CNRMF))) THEN
     INFO = 1
     RETURN
  END IF
  DO I = 2, M
     CNRMF = CR_HYPOT(CNRMF, CR_HYPOT(REAL(X(I)), AIMAG(X(I))))
     IF (.NOT. (CNRMF .LE. HUGE(CNRMF))) THEN
        INFO = I
        RETURN
     END IF
  END DO
END FUNCTION CNRMF
