PROGRAM STH2T
  USE, INTRINSIC :: IEEE_ARITHMETIC, ONLY: IEEE_FMA
  USE, INTRINSIC :: ISO_FORTRAN_ENV, ONLY: ERROR_UNIT, OUTPUT_UNIT, REAL32, REAL128
  IMPLICIT NONE
  INTERFACE
     SUBROUTINE SRANDOM(S) BIND(C,NAME='srandom')
       USE, INTRINSIC :: ISO_C_BINDING, ONLY: c_int
       IMPLICIT NONE
       INTEGER(KIND=c_int), INTENT(IN), VALUE :: S
     END SUBROUTINE SRANDOM
     FUNCTION RANDOM() BIND(C,NAME='random')
       USE, INTRINSIC :: ISO_C_BINDING, ONLY: c_long
       IMPLICIT NONE
       INTEGER(KIND=c_long) :: RANDOM
     END FUNCTION RANDOM
     PURE FUNCTION CR_RSQRT(X) BIND(C,NAME='cr_rsqrtf')
       USE, INTRINSIC :: ISO_C_BINDING, ONLY: c_float
       IMPLICIT NONE
       REAL(KIND=c_float), INTENT(IN), VALUE :: X
       REAL(KIND=c_float) :: CR_RSQRT
     END FUNCTION CR_RSQRT
  END INTERFACE
  REAL(KIND=REAL128), PARAMETER :: QZERO = 0.0_REAL128, QONE = 1.0_REAL128
  REAL(KIND=REAL32), PARAMETER :: ZERO = 0.0_REAL32, ONE = 1.0_REAL32
  REAL(KIND=REAL32), PARAMETER :: CUTOFF = 40.0_REAL32 / 41.0_REAL32, DEPS = EPSILON(ZERO) / 2
  INTEGER, PARAMETER :: NEXP = 1 - EXPONENT(DEPS), ETH = 1, ECH = 2, ESH = 3, ARE = 1, MRE = 2
  CHARACTER(LEN=256) :: CLA
  REAL(KIND=REAL128) :: Q(2,3,NEXP), QD, QT, QC, QS, QE
  REAL(KIND=REAL32) :: D, T, C, S
  INTEGER :: NSAMP(NEXP)
  INTEGER, ALLOCATABLE :: ISEED(:)
  !DIR$ ATTRIBUTES ALIGN: 64:: ISEED
  INTEGER :: I, N, SSIZE
  ! random seed may be given
  CALL RANDOM_SEED(SIZE=SSIZE)
  IF (SSIZE .LE. 0) STOP 'seed size non-positive'
  I = COMMAND_ARGUMENT_COUNT()
  IF (I .LT. 1) THEN
     IF (SSIZE .GT. 1) THEN
        WRITE (CLA,'(I1)') SSIZE
        CLA = 'args: N [SEED1 ... SEED'//TRIM(CLA)//']'
     ELSE ! SSIZE = 1
        CLA = 'args: N [SEED1]'
     END IF
     WRITE (ERROR_UNIT,*) TRIM(CLA)
     STOP 'All SEED arguments have to be given, or none of them.'
  END IF
  CALL GET_COMMAND_ARGUMENT(1, CLA)
  READ (CLA,*) N
  IF (I .EQ. 1) THEN
     ALLOCATE(ISEED(SSIZE))
     CALL RANDOM_SEED
     CALL RANDOM_SEED(GET=ISEED)
  ELSE IF (I .EQ. (SSIZE + 1)) THEN
     ALLOCATE(ISEED(SSIZE))
     DO I = 1, SSIZE
        CALL GET_COMMAND_ARGUMENT(I + 1, CLA)
        READ (CLA,*) ISEED(I)
     END DO
     CALL RANDOM_SEED(PUT=ISEED)
  ELSE ! a wrong SEED
     STOP 'invalid number of SEED arguments'
  END IF
  DO I = 1, SSIZE-1
     WRITE (ERROR_UNIT,'(I12)',ADVANCE='NO') ISEED(I)
  END DO
  WRITE (ERROR_UNIT,'(I12)') ISEED(SSIZE)
  CALL SRANDOM(ISEED(1))
  ISEED = 0
  Q = QZERO
  D = ZERO
  NSAMP = 0
  DO I = 1, ABS(N)
1    CALL RANDOM_NUMBER(D)
     IF (.NOT. (D .GE. TINY(ZERO))) GOTO 1
     D = FRACTION(D)
     SSIZE = -MOD(ABS(INT(RANDOM())), NEXP)
     D = SCALE(D, SSIZE)
     IF (D .GE. CUTOFF) THEN
        ISEED(1) = ISEED(1) + 1
        IF (N .LT. 0) GOTO 1
     END IF
     SSIZE = 1 - EXPONENT(D)
     NSAMP(SSIZE) = NSAMP(SSIZE) + 1
     T = D / (ONE + SQRT(IEEE_FMA(-D, D, ONE)))
     C = CR_RSQRT(IEEE_FMA(-T, T, ONE))
     S = C * T
     QD = D
     QT = QD / (QONE + SQRT(IEEE_FMA(-QD, QD, QONE)))
     QS = SQRT(IEEE_FMA(-QT, QT, QONE))
     QC = QONE / QS
     QS = QT / QS
     QD = NSAMP(SSIZE) - 1
     QD = QD / NSAMP(SSIZE)
     QE = T
     QE = ABS(QT - QE) / QT
     Q(ARE,ETH,SSIZE) = Q(ARE,ETH,SSIZE) * QD + QE / NSAMP(SSIZE)
     Q(MRE,ETH,SSIZE) = MAX(Q(MRE,ETH,SSIZE), QE)
     QE = C
     QE = ABS(QC - QE) / QC
     Q(ARE,ECH,SSIZE) = Q(ARE,ECH,SSIZE) * QD + QE / NSAMP(SSIZE)
     Q(MRE,ECH,SSIZE) = MAX(Q(MRE,ECH,SSIZE), QE)
     QE = S
     QE = ABS(QS - QE) / QS
     Q(ARE,ESH,SSIZE) = Q(ARE,ESH,SSIZE) * QD + QE / NSAMP(SSIZE)
     Q(MRE,ESH,SSIZE) = MAX(Q(MRE,ESH,SSIZE), QE)
  END DO
  IF (N .LT. 0) WRITE (ERROR_UNIT,'(A,I11)') 'Skipped:', ISEED(1)
  DEALLOCATE(ISEED)
  ! relative errors in the terms of \epsilon
  QE = DEPS
  Q = Q / DEPS
#ifndef NDEBUG
  WRITE (OUTPUT_UNIT,'(A)') '"EXP(TH(2φ))", "NSAMP", "AVG(ρ(TH))/ε", "MAX(ρ(TH))/ε", "AVG(ρ(CH))/ε", "MAX(ρ(CH))/ε", "AVG(ρ(SH))/ε", "MAX(ρ(SH))/ε"'
#endif
  DO I = NEXP, 1, -1
     SSIZE = -I
     WRITE (OUTPUT_UNIT,'(I3,A,I11,6(A,ES16.9E2))') SSIZE, ',', NSAMP(I), &
          ',', Q(ARE,ETH,I), ',', Q(MRE,ETH,I), &
          ',', Q(ARE,ECH,I), ',', Q(MRE,ECH,I), &
          ',', Q(ARE,ESH,I), ',', Q(MRE,ESH,I)
  END DO
END PROGRAM STH2T
